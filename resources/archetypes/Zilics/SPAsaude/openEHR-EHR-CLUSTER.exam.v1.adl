archetype (adl_version=1.4)
	openEHR-EHR-CLUSTER.exam.v1

concept
	[at0000]	-- Examination
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"unknown">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"A generic cluster for recording findings on physical examination.">
			use = <"">
			keywords = <"examination", "physical">
			misuse = <"">
			copyright = <"copyright (c) 2010 openEHR foundation">
		>
	>
	lifecycle_state = <"Initial">
	other_contributors = <>

definition
	CLUSTER[at0000] matches {	-- Examination
		items cardinality matches {0..*; unordered} matches {
			CLUSTER[at0004] occurrences matches {0..1} matches {	-- Normal statements
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0005] occurrences matches {0..*} matches {	-- Normal statement
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {	-- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0008] occurrences matches {0..1} matches {	-- Findings
				items cardinality matches {0..*; unordered} matches {
					CLUSTER[at0003] occurrences matches {0..1} matches {	-- Detail
						items cardinality matches {0..*; unordered} matches {
							allow_archetype CLUSTER occurrences matches {0..1} matches {
								include
									archetype_id/value matches {/exam-generic\.v1/}
							}
						}
					}
				}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {	-- Image
				value matches {
					DV_MULTIMEDIA matches {
						media_type matches {
							[openEHR::
							426, 
							427, 
							429]
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination">
					description = <"Basic examination cluster for describing findings on physical examination when there is no named focus of examination">
				>
				["at0002"] = <
					text = <"Clinical description">
					description = <"Textural description of the part examined">
				>
				["at0003"] = <
					text = <"Detail">
					description = <"More focused examination findings">
				>
				["at0004"] = <
					text = <"Normal statements">
					description = <"A group of statements about the normality of the examination">
				>
				["at0005"] = <
					text = <"Normal statement">
					description = <"A specific statement of normality">
				>
				["at0006"] = <
					text = <"Image">
					description = <"Drawing or image of the area examined">
				>
				["at0008"] = <
					text = <"Findings">
					description = <"Clinical findings">
				>
			>
		>
	>
